plugins {
    id 'java-library'
    id 'maven-publish'
    id 'biz.aQute.bnd.builder'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        getByName('maven').pom.description = 'Quartz Enterprise Job Scheduler'
    }
}

dependencies {
    compileOnly project(':quartz-stubs')

    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    runtimeOnly "org.slf4j:slf4j-log4j12:$slf4jVersion"
    implementation "com.mchange:c3p0:$c3p0Version"
    implementation "com.zaxxer:HikariCP:$hikaricpVersion"
    compileOnly "jboss:jboss-common:$jbossVersion"
    compileOnly "jboss:jboss-minimal:$jbossVersion"
    compileOnly "jboss:jboss-system:$jbossVersion"
    compileOnly "jboss:jboss-jmx:$jbossVersion"
    compileOnly "org.apache.geronimo.specs:geronimo-commonj_1.1_spec:$commonjVersion"

    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.4'

    compileOnly 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    testImplementation 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.1.0'

    testImplementation "org.apache.derby:derby:$derbyVersion"
    testImplementation "org.apache.derby:derbyclient:$derbyVersion"
    testImplementation "org.apache.derby:derbynet:$derbyVersion"
    testImplementation 'asm:asm:3.3.1'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation(platform("org.junit:junit-bom:5.11.4"))
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation "org.testcontainers:mssqlserver:$testcontainersVersion"
    testImplementation "org.testcontainers:mariadb:$testcontainersVersion"
    testImplementation "org.testcontainers:postgresql:$testcontainersVersion"
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc:13.2.1.jre11'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client:3.5.6'
    testImplementation 'org.postgresql:postgresql:42.7.8'
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

test {
    useJUnitPlatform(){
        includeEngines("junit-jupiter")
    }
    maxParallelForks 1
    forkEvery 1
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        docTitle = "Quartz Enterprise Job Scheduler $project.version API"
        encoding = 'UTF-8'
        outputLevel = JavadocOutputLevel.QUIET
        splitIndex = false
        author = true;
        windowTitle = "Quartz Enterprise Job Scheduler $project.version API"
        bottom = 'Copyright IBM Corp. 2024, 2025'
    }
}


processResources {
    filesMatching('**/quartz-build.properties') {
        expand([version: project.version, fullname: project.fullname, name: project.name])
    }
}
